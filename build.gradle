buildscript {
    repositories {
        maven {
            url 'http://maven.ej-technologies.com/repository'
        }
    }
    dependencies {
        classpath group: 'com.install4j', name: 'install4j-gradle', version: '6.1'
    }
}

plugins {
    id "de.undercouch.download" version "3.1.1"
}

apply plugin: 'java'
apply plugin: 'install4j'


defaultTasks 'build'

repositories {
    mavenLocal()
}

def extractDirectory = "${buildDir}/dist"

dependencies {
    compile('org.openthinclient:manager-runtime-standalone:2.0.0-SNAPSHOT:distribution@tar.gz')
}

install4j {
    installDir = file(install4jHomeDir)
}

/**
 * Extracts the distribution into the extractDirectory
 */
task extract(type: Copy) {
    configurations.compile.each {
        from tarTree(it)
    }
    into extractDirectory
}

task installer(type: com.install4j.gradle.Install4jTask) {
    dependsOn 'extract'

    projectFile = 'openthinclient-manager.install4j'
}

task prepareVagrantTest << {

    file("test/data/java").mkdirs();
    file("test/data/installers").mkdirs();

    copy {
        from "${buildDir}/installers"
        into file("test/data/installers")
    }

    download {
        header "Cookie", "oraclelicense=accept-securebackup-cookie"
        acceptAnyCertificate true
        src "http://download.oracle.com/otn-pub/java/jdk/8u102-b14/jre-8u102-windows-x64.exe"
        dest file("test/data/java")
    }

}

task cleanVagrantTestData(type: Delete) {
    delete fileTree(dir:'test/data/', excludes: ['java/*.properties'])
}
prepareVagrantTest.dependsOn 'installer'
clean.dependsOn cleanVagrantTestData
build.dependsOn 'installer'